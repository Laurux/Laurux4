' Gambas class file

' Gambas class file

'----------------------------------------------------------------------------
'

'
'Ce programme est un logiciel libre; Vous pouvez le redistribuer et/ou
'le modifier selon les termes de la GNU General PUBLIC License publiés par
'la Free Software Foundation.

'Ce programme est distribué sans garantie. Voyez la GNU General PUBLIC
'License pour plus de details.

'Vous devez lire la GNU General PUBLIC Licence.
'Si vous ne disposez pas d'un exemplaire, veuillez écrire
'à " The Free Software Foundation
'INC, 59 Temple place - Suite 33, Boston
'MA 02111-1307 USA
'----------------------------------------------------------------------------
'################################################
'# nom du fichier           : Txmo.class
'# Auteur                   : Jacky Tripoteau
'# date de création         : 20/11/2004
'# Gestion de la table des taux de Main d'oeuvre
'################################################
'

son As Integer = Start.Son
b As Integer
bb As Integer
Montante As String
Montantp As String
MargeMo As Float

Public Sub _New()
  
  If Settings["/Soc" & Start.Societe & "/Coul_fen"] Then Utils.Observers(Me)
  
  Music.Load(Start.Musique)
  Me.Center
  Coltxmo.Columns.count = 4
  Coltxmo.Columns[0].Width = 60
  Coltxmo.Columns[1].Width = 60
  Coltxmo.Columns[1].Alignment = 2
  Coltxmo.Columns[2].Width = 175
  Coltxmo.Columns[3].Width = 70
  Coltxmo.Columns[0].Text = "Cd"
  Coltxmo.Columns[1].Text = "Taux"
  Coltxmo.Columns[2].Text = "libellé"
  Coltxmo.Columns[3].Text = "compte"
  CdTx.SetFocus
  
End

Public Sub Form_Open()
  
  Dim rResult As Result
  
  rResult = Utils.db.Exec("SELECT * FROM " & Cbase.Table("TabTxmo") & " order by code_txtmo")
  If rResult.Available Then
    Repeat
      Coltxmo.Add(rResult!code_txtmo, rResult!code_txtmo)
      Coltxmo.Item[0] = rResult!code_txtmo
      Coltxmo.Item[1] = Format$(rResult!tx_txtmo, "0.00")
      Coltxmo.Item[2] = rResult!libell_txtmo
      Coltxmo.Item[3] = rResult!compt_txtmo
      Coltxmo.Item[4] = rResult!prvt_txtmo
    Until rResult.MoveNext()
    'Maj_Zones()
  Else
  Endif
  
End

Public Sub Refresh()
  
  Coltxmo.clear
  Cleanchamps()
  form_Open()
  Maj_Zones
  
End

Public Sub Maj_Zones()
  
  CdTx.Text = Coltxmo.Current[0]
  txtmo.Text = Coltxmo.Current[1]
  LibelTx.text = Coltxmo.current[2]
  codeCompt.text = Coltxmo.current[3]
  Prvt.text = Format$(Val(Utils.cpoint(Coltxmo.current[4])), "0.00")
  CdTx.SetFocus
Catch
  
End

Public Sub Cleanchamps()
  
  CdTx.Clear
  txtmo.Clear
  LibelTx.Clear
  codecompt.Clear
  Prvt.Text = "0,00"
  
End

Public Sub Btn_Click()
  
  Select Case Last.tag
      
    Case 1
      Button5_Click()
      
    Case 2
      Button3_Click()
      
    Case 3
      Button1_Click()
      
    Case 4
      Me.Close
      
  End Select
  
End

Public Sub Button1_Click()
  
  Dim rResult As Result
  Dim TAB As String
  Dim Tx As Float
  Dim sKey As String
  
  Tab = "Fiches_Txtmo" 
  b = 0
  With Utils
    compteGman()
    
    If Not IsNull(txtmo.Text) 
      If b <> 1 Then
        If CdTx.TEXT = "" Or codeCompt.text = "" Then
          If son Then
            Music.Play
          Endif
          If Message.Question("VOUS DEVEZ D'ABORD REMPLIR LES CHAMPS OBLIGATOIRES", "OK") = 1 Then
          Endif
        Else
          Tx = Val(.cpoint(txtmo.Text))
          rResult = Utils.db.Exec("SELECT * FROM " & tab & " WHERE code_txtmo = &1", CdTx.Text)
          If rResult.Available Then
            Utils.db.Exec("UPdate  " & tab & "  SET  code_txtmo = &1 , libell_txtmo = &2 , compt_txtmo = &3, tx_txtmo = &4, prvt_txtmo = &5 WHERE code_txtmo = &1", CdTx.Text, LibelTx.Text, codecompt.Text, Tx, Val(Prvt.Text))
            sKey = Coltxmo.Current.Key
          Else
            Utils.db.Exec("INSERT INTO " & tab & "(code_txtmo,libell_txtmo,compt_txtmo, tx_txtmo, prvt_txtmo) VALUES (&1, &2, &3, &4, &5)", CdTx.Text, LibelTx.Text, codeCompt.Text, Tx, Val(Prvt.Text))
            sKey = CdTx.Text
          Endif
        Endif
        If Montante <> Txtmo.Text Or If Montantp <> Prvt.text Then MajMo()
        Refresh()
        Cleanchamps()
        Coltxmo.MoveTo(sKey)
        Coltxmo.Item.Selected = True
        Coltxmo.Item.EnsureVisible
        CdTx.SetFocus
        CdTx.Select
      Else
        codecompt.Select
        codecompt.SetFocus
        b = 0
      Endif
    Else
      Txtmo.Select
      Txtmo.SetFocus
      b = 0
    Endif
  End With
  
End

Public Sub MajMo()
  
  Dim MoResult As Result
  
  With utils
    MoResult = Utils.db.Exec("SELECT * FROM " & Cbase.Table("TabMo") & " WHERE mo_txcd = &1", CdTx.Text)
    If MoResult.Available Then
      Repeat
        Calc_MargeMo(MoResult!mo_code)
        Utils.db.Exec("UPdate  " & Cbase.Table("TabMo") & "  SET  mo_txht = &2, mo_prvt = &3, mo_marge = &4 where mo_code = &1", MoResult!mo_code, Val(.cpoint(txtmo.Text)), Val(Prvt.Text), MargeMo)
      Until Moresult.MoveNext()
    Endif
  End With
  
End

Public Sub Calc_MargeMo(Code As String)
  
  Dim Rmo As Result
  Dim Tx As Float
  Dim txh As Float
  Dim Txf As Float
  Dim fvaleur As Float
  
  rmo = Utils.db.Exec("select * from " & Cbase.Table("TabMo") & " where mo_code = &1", code)
  If rmo!mo_tempmont = "1" Then
    fvaleur = rmo!mo_montant
    'On calcule le montant du taux horaire
    Txh = Val(Utils.cpoint(Txtmo.Text)) * fvaleur
    'Puis le montant du prix de revient
    Txf = Val(Utils.cpoint(Prvt.Text)) * fvaleur
    'On calcule la marge en valeur pour finir.
    MargeMo = Txh - Txf
  Else
    'On calcule à quoi correspond le montant du forfait
    Tx = rmo!mo_valeurht / Val(Utils.cpoint(Txtmo.Text))
    'Puis on calcule le montant du taux horaire
    Txh = Val(Utils.cpoint(Txtmo.Text)) * tx
    'Puis le montant du prix de revient
    Txf = Val(Utils.cpoint(Prvt.Text)) * tx
    'On calcule la marge en valeur pour finir.
    MargeMo = Txh - Txf
  Endif
Catch
  MargeMo = 0
  
End

Public Sub Coltxmo_Click()
  
  Cleanchamps()
  Maj_Zones
  
End

Public Sub Button3_Click()
  
  Dim rResult As Result
  Dim Moresult As Result
  Dim TAB As String
  
  Tab = "Fiches_Txtmo" 
  If son Then
    Music.Play
  Endif
  If Not IsNull(CdTx.Text) Then
    If Message.Question("Etes vous sur de vouloir effacer cet enregistrement", "Oui", "Non") = 1 Then
      MoResult = Utils.db.Exec("SELECT * FROM " & Cbase.Table("TabMo") & " WHERE mo_txcd = &1", CdTx.Text)
      If MoResult.Available Then
        Message.Error("Suppression impossible ! ce taux de MO est utilisé par le code MO : " & Moresult!mo_code)
      Else
        Utils.db.Exec("DELETE FROM " & tab & " WHERE code_txtmo = &1", Coltxmo.Current[0])
        Message.Info("Taux de MO supprimé")
        Refresh()
      Endif
      Me.Raise
    Endif
  Endif
  
End

Public Sub Button4_Click()
  
  Dim rResult As Result
  Dim TAB As String
  
  Tab = "Fiches_Comptes" 
  If Colcompt.visible Then
    Colcompt.clear
    Colcompt.visible = False
  Else
    Colcompt.visible = True
    Colcompt.Columns.count = 2
    Colcompt.Columns[0].Width = 65
    Colcompt.Columns[1].Width = 180
    Colcompt.Columns[0].Text = "code"
    Colcompt.Columns[1].Text = "Intitulé"
    rResult = Utils.db.Exec("SELECT * FROM " & tab & " where left(compte_cc,1) = '7' order by cast(compte_cc AS char)")
    If rResult.Available Then
      Repeat
        Colcompt.Add(rResult!compte_cc, rResult!compte_cc)
        Colcompt.Item[0] = rResult!compte_cc
        Colcompt.Item[1] = rResult!intitule_cc
      Until rResult.MoveNext()
      Colcompt.MoveFirst
      Colcompt.SetFocus
      Colcompt.Item.Selected = True
    Endif
  Endif
  
End

Public Sub Colcompt_Click()
  
  codecompt.text = Colcompt.Item[0]
  Colcompt.visible = False
  Colcompt.clear
  codecompt.SetFocus
  codecompt.Select
  
End

'**********************************************************
'* Gestion du columnview lors d'une saisie manuelle       *
'**********************************************************
Public Sub Colcompt_KeyPress()
  
  If Key.code = Key.Enter Or Key.code = Key.Return Then
    Colcompt.MoveCurrent
    Colcompt_Click()
    Stop Event
  Endif
  
  If key.code = key.F1 Then
    Button5_Click()
    Stop Event
  Endif
  
  If Key.code = Key.Escape Or Key.code = Key.F2 Then
    Colcompt.visible = False
    Colcompt.clear
    codecompt.SetFocus
    codecompt.Select
    Stop Event
  Endif
  
End

'****************************************************
'*      Gestion du focus. Routine de Charlie Reinl  *
'****************************************************
Public Sub Tmo_GotFocus()
  
  With Utils
    .SetEditColor(Me, Last)
  End With
  Select Case Last.tag
    Case 3
      Montante = txtmo.Text
      
    Case 5
      Montantp = prvt.Text
  End Select
  
End

Public Sub codecompt_LostFocus()
  
  codecompt.Background = Color.TextBackground
  compteGman()
  
End

Public Sub txtmo_LostFocus()
  
  Dim Tx As Float
  
  With utils
    txtmo.Text = .cpoint(txtmo.Text)
    If Val(txtmo.text) = Null Then
      If son Then
        Music.Play
      Endif
      message.Warning("Veuillez verifier votre saisie SVP !")
    Else
      Tx = Val(.cpoint(txtmo.Text))
      txtmo.Text = Format$(Tx, "0.00")
    Endif
    If txtmo.Text = "" Then
      If son Then
        Music.Play
      Endif
      Message.Warning(" Attention ! Veuillez saisir un montant avant de valider votre ligne SVP !")
    Endif
  End With
  
End

Public Sub Prvt_LostFocus()
  
  Dim Tx As Float
  
  With utils
    Prvt.Text = .cpoint(Prvt.Text)
    If Val(Prvt.text) = Null Then
      If son Then
        Music.Play
      Endif
      message.Warning("Veuillez verifier votre saisie SVP !")
      b = 1
    Else
      Tx = Val(.cpoint(Prvt.Text))
      Prvt.Text = Format$(Tx, "0.00")
    Endif
    If Prvt.Text = "" Then
      If son Then
        Music.Play
      Endif
      If b <> 1 Then
        Message.Warning(" Attention ! Veuillez saisir un montant avant de valider votre ligne SVP !")
        b = 1
      Endif
    Endif
  End With
  
End

Public Sub Tmo_KeyPress()
  
  If Key.code = Key.enter Or Key.code = Key.Return Or Key.code = Key.Tab Then
    Select Case Last.tag
        
      Case 1
        LibelTx.SetFocus
        LibelTx.Select
        Stop Event
        
      Case 2
        txtmo.SetFocus
        txtmo.Select
        Stop Event
        
      Case 3
        txtmo_LostFocus()
        codeCompt.SetFocus
        codeCompt.Select
        Stop Event
        
      Case 4
        codecompt_LostFocus()
        If b = 1 Then
          codeCompt.SetFocus
          codeCompt.Select
          b = 0
        Else
          Prvt.Setfocus
          Prvt.select
        Endif
        Stop Event
        
      Case 5
        Prvt_LostFocus()
        If b = 1 Then
          Prvt.SetFocus
          Prvt.Select
          b = 0
        Else
          Cdtx.Setfocus
        Endif
        Stop Event
    End Select
  Endif
  
  If key.code = key.F1 Then
    Button5_Click()
    Stop Event
  Endif
  
  If key.code = key.F2 Then
    Select Case Last.tag
      Case 4
        Button4_Click()
        Stop Event
        
    End Select
  Endif
  
End

'******************************************************************
'*  Recherche d'un compte vente lors d'une saisie manuelle      *
'******************************************************************
Public Sub compteGman()
  
  Dim Cmpts As Result
  Dim TAB As String
  
  Tab = "Fiches_Comptes" 
  Cmpts = Utils.db.Exec("SELECT * FROM " & Tab & "  WHERE compte_cc = &1", codecompt.Text)
  If Not Cmpts.Available Then
    If son Then
      Music.Play
    Endif
    message.Warning("Ce compte vente n'existe pas !\n Verifiez votre saisie SVP !")
    b = 1
  Endif
  
End

'**********************************
'*    Appel de la doc html        *
'**********************************
Public Sub Button5_Click()
  
  Exec ["xdg-open", Application.Path &/ "Ecrans/Txmo.html"]
  
End
