' Gambas class file

Nbdec As String = "0.00"
Ref As String
Intitule As String
PbHT As String ' Prix de base
PaHT As String 'Prix achat en UV
Txconv As String ' Taux de conversion
Paua As String ' Prix d'acaht en UA = prix d'achat/taux de conversion
Prvt As String 'Prix de revient = Paht + frais
Pvht As String ' Prix de vente HT  = Paua * Coefficient de vente
Pvttc As String ' Prix de vente TTC
Pmp As String 'Prix moyen pondéré
Stocke As String ' si le produit est stocké
Margem As String ' Marge en montant
Margep As String 'Marge en pourcentage
arr As String ' Arrondi (sert à arrondir le prix de vente TTC)
Remise As String
Coeff As String ' Coefficient de vente
Percent As String ' Taux de marque
Mtva As String ' Taux de la TVA
Cbarre As String
Cequ As String
Poids As String
Copie As String
Ecot As String
Code As String
Frais As String
Divers As String
son As Integer = Start.Son

Public Sub _new()
  
  If Settings["/Soc" & Start.Societe & "/Coul_fen"] Then Utils.Observers(Me)
  Music.Load(Start.Musique)
  Me.Center
  
End

Public Sub Button3_Click()
  
  Me.close
  
End

Public Sub Button2_Click()
  
  Dim rArt As Result
  Dim i As Integer = 0
  
  With Utils
    rArt = Utils.db.Exec("SELECT * FROM " & Cbase.Table("TabArt") & " WHERE art_datefbt = &1", .Cdate_Dbase(date.text))
    If rArt.Available Then
      Message.Warning("Attention ! Durant cette opération les autres utilisateurs \n n'auront plus accès à la table des articles.")
      Utils.db.Exec("LOCK TABLES " & Cbase.Table("TabArt") & " WRITE, " & Cbase.Table("TabTvav") & " Write")
      Me.mouse = Mouse.Wait
      i = rArt.count
      Repeat
        Paua = rArt!art_pbfbt
        nbdec = Rart!art_nbd
        nbdec = .Find_nbdec(nbdec)
        Remise = Rart!art_tr
        Txconv = Rart!art_txconv
        Frais = rArt!art_frais
        Coeff = rArt!art_coef
        Mtva = rArt!art_tva
        rTva()
        If Tbrut.Value = True Then
          Prix_base()
        Else
          Prix_achat()
        Endif
        Calc_Percent()
        Calc_coeff()
        Utils.db.Exec("UPdate  " & Cbase.Table("TabArt") & "  SET  art_pbht = &1, art_tr = &2, art_pauaht =&3, art_pvht = &4, art_pvttc = &5, art_prvt = &6, art_paht = &7, art_coef = &8, art_pbfbt = &9, art_datefbt = &{10},  art_ddatefbt = &{11} WHERE art_code = &{12}", Val(pbht), Val(remise), Val(Paua), Val(Pvht), Val(Pvttc), Val(Prvt), Val(paht), Val(Coeff), "", "", Format$(Now, "yyyymmdd"), rArt!art_code)
      Until rArt.MoveNext()
      Utils.db.Exec("UNLOCK TABLES")
      Me.Mouse = mouse.Default
      Message.Info("Traitement terminé ! " & i & " références ont été mises à jour pour la date future base tarif du " & date.text & " !")
    Else
      Message.Info("Aucune mise à jour n'est à faire pour cette date !")
    Endif
  End With
  
End

Public Sub ToggleButton2_Click()
  
  If DateChooser1.Visible = False Then
    DateChooser1.visible = True
  Else
    DateChooser1.Visible = False
  Endif
  
End

Public Sub DateChooser1_Activate()
  
  Date.text = Format$(DateChooser1.Value, "dd.mm.yyyy")
  DateChooser1.Visible = False
  
End

'***********************************
'*   On gère le prix de base ht    *
'***********************************
Public Sub prix_base()
  
  With Utils
    pbht = PaUA
    pbht = Format$(Val(.cpoint(pbht)), nbdec)
    Trc()
    paht = Format$(Val(PaUA) / Val(.cpoint(Txconv)), nbdec)
    Prvt = Format(Val(paht) + Val(.cpoint(Frais)), nbdec)
    Pvht = Format$(Val(prvt) * Val(.cpoint(Coeff)), nbdec)
    Tva_Calcul()
    PvTTC_arrondi()
    PvTTcLF()
    Calc_Marge()
  End With
Catch
  If son Then
    Music.Play
  Endif
  message.Error(Error.Text & " " & Error.where)
  
End

'***********************************
'*  On gère le prix de vente HT    *
'***********************************
Public Sub PvTTcLF()
  
  Dim tx As Float
  
  tx = (1 + (Val(Utils.cpoint(Mtva)) / 100))
  Pvht = Format$(Val(PvTTc) / tx, nbdec)
  
End

'*********************************************
'*    On gère le taux de remise fournisseur  *
'*********************************************
Public Sub TrC()
  
  With Utils
    remise = .cpoint(remise)
    If Val(remise) = Null Then remise = "0"
    remise = Format$(Val(remise), "0.00")
    PaUA = Format$(Val(PbHT) - (Val(PbHT) * Val(remise) / 100), Nbdec)
  End With
Catch
  If son Then
    Music.Play
  Endif
  message.Error(Error.Text & " " & Error.where)
  
End

Public Sub Prix_achat()
  
  With Utils
    PaUA = .cpoint(PaUA)
    If Val(PaUA) = Null Then PaUA = "0"
    PaUA = Format$(Val(PaUA), nbdec)
    If Val(PaUA) <> "0" Then
      pbht = Format$(Val(PaUA) / (1 - (Val(remise) / 100)), nbdec)
      paht = Format$(Val(PaUA) / Val(Txconv), nbdec)
      Prvt = Format(Val(paht) + Val(Frais), nbdec)
      Pvht = Format$(Val(prvt) * Val(Coeff), nbdec)
      Tva_Calcul()
      PvTTC_arrondi()
      PvTTcLF()
      Calc_Marge()
    Endif
  End With
Catch
  If son Then
    Music.Play
  Endif
  message.Error(Error.Text & " " & Error.where)
  
End

'*****************************************
'*      Récupération de la tva           *
'*****************************************
Public Sub rTva()
  
  Dim Rtvaav As Result
  
  Rtvaav = Utils.db.Exec("SELECT * FROM " & Cbase.Table("TabTvav") & " WHERE code_tva = &1", Mtva)
  If Rtvaav.Available Then
    Mtva = Rtvaav!taux_tva
  Endif
  
End

'***************************************************************
'*   On calcule le montant de la tva et le prix de vente TTC   *
'***************************************************************
Public Sub Tva_Calcul()
  
  With Utils
    If PvTTc <> "" Or Pvht <> "" Then
      Mtva = Format$(Val(.cpoint(Mtva)), "00.00")
      PvTTc = Format$(Val(Pvht) + (Val(Pvht) * Val(Mtva) / 100), nbdec)
    End If
  End With
  
End

'***********************************
'*       On calcule la marge       *
'***********************************
Public Sub Calc_Marge()
  
  If IsNull(Pmp) Then Pmp = prvt
  If stocke Then
    Margem = Format$(Val(Pvht) - Val(Pmp), nbdec)
    Try Margep = Format$((Val(Margem) * 100) / Val(Pmp), nbdec)
  Else
    If Val(prvt) <> 0 Then
      Margem = Format$(Val(Pvht) - Val(prvt), nbdec)
      Try Margep = Format$((Val(Pvht) - Val(Pmp)) / Val(Pvht) * 100, nbdec)
    Endif
  Endif
  
End

'***********************************
'*        On gère l' arrondi       *
'***********************************
Public Sub PvTTC_arrondi()
  
  If Not PvTTc Then PvTTc = "0,00"
  Pvttc = Utils.cpoint(Pvttc)
  If arr = "0,05" Then
    If Right$(pvTTc) Like "[34567]*" Then
      PvTTC = Left$(PvTTC, (Len(PvTTC) - 1)) & "5"
    Else
      PvTTC = Round(Val(PvTTC), -1)
      PvTTC = Format$(PvTTC, nbdec)
    Endif
  Endif
  
  If arr = "0,10" Then
    PvTTC = Round(Val(PvTTC), -1)
    PvTTC = Format$(PvTTC, nbdec)
  Endif
  
  If arr = "0,50" Then
    If Val(pvTTc) <= Val(Left$(pvTTc, (Len(pvTTc) - 2)) & 25) Then
      PvTTC = Left$(PvTTC, (Len(PvTTC) - 2)) & "00"
    Else
      If Val(pvTTc) <= Val(Left$(pvTTc, (Len(pvTTc) - 2)) & 75) Then
        PvTTC = Left$(PvTTC, (Len(PvTTC) - 2)) & "50"
      Endif
      If Val(pvTTc) >= Val(Left$(pvTTc, (Len(pvTTc) - 2)) & 76) Then
        PvTTC = Round(Val(PvTTC))
        PvTTC = Format$(PvTTC, nbdec)
      Endif
    Endif
  Endif
  
  If arr = "1,00" Then
    PvTTC = Round(Val(PvTTC))
    PvTTC = Format$(PvTTC, nbdec)
  Endif
  
End

'********************************************
'*       On calcule le taux de marque       *
'********************************************
Public Sub Calc_Percent()
  
  Try Percent = Format$(((Val(Pvht) - Val(prvt)) / Val(Pvht)) * 100, "0.000")
  
End

'***************************************************
'*        On calcule le coefficient de marge       *
'***************************************************
Public Sub Calc_coeff()
  
  Try Coeff = Format$(1 / (1 - (Val(Percent)) / 100), "0.0000")
  
End

'***************************************
'*      Appel de la documentation      *
'***************************************
Public Sub Button1_Click()
  
  Exec ["xdg-open", Application.Path &/ "Ecrans/Majtarif.html"]
  
End
