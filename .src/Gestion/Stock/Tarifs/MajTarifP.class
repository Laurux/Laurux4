' Gambas class file

Nbdec As String = "0.00"
Ref As String
Intitule As String
PbHT As String ' Prix de base
PaHT As String 'Prix achat en UV
Txconv As String ' Taux de conversion
Paua As String ' Prix d'acaht en UA = prix d'achat/taux de conversion
Prvt As String 'Prix de revient = Paht + frais
Pvht As String ' Prix de vente HT  = Paua * Coefficient de vente
Pvttc As String ' Prix de vente TTC
Pmp As String 'Prix moyen pondéré
Stocke As String ' si le produit est stocké
Margem As String ' Marge en montant
Margep As String 'Marge en pourcentage
arr As String ' Arrondi (sert à arrondir le prix de vente TTC)
Remise As String
Coeff As String ' Coefficient de vente
Percent As String ' Taux de marque
Mtva As String ' Taux de la TVA
Cbarre As String
Cequ As String
Poids As String
Copie As String
Ecot As String
Code As String
Frais As String
Divers As String
b As Integer = 0
Etiqb As Boolean ' Si true on demande si on veut imprimer les etiquettes
son As Integer = Start.Son

Public Sub _new()
  
  If Settings["/Soc" & Start.Societe & "/Coul_fen"] Then Utils.Observers(Me)
  Music.Load(Start.Musique)
  Me.Center
  
End

Public Sub Button3_Click()
  
  Me.Close
  
End

'****************************************************
'*      Gestion du focus. Routine de Charlie Reinl  *
'****************************************************
Public Sub Cmpt_GotFocus()
  
  With Utils
    .SetEditColor(Me, Last)
    Last.SELECT
  End With
  
End

Public Sub Cmpt_keyPress()
  
  Select Case Last.tag
      
    Case 1
      If Key.code = Key.enter Or Key.code = Key.Return Or Key.code = Key.Tab Then
        If Not IsNull(Four.text) Then
          fourMan()
          If b = 1 Then
            Four.SetFocus
            Four.Select
            b = 0
          Else
            Pourcent.SetFocus
          Endif
          Stop Event
        Endif
      Endif
      
    Case 2
      ChkPrx()
      If Key.code = Key.enter Or Key.code = Key.Return Or Key.code = Key.Tab Then
        Pourcent.Text = Format$(Val(Utils.cpoint(Pourcent.text)), "0.00")
        If Val(Utils.cpoint(pourcent.text)) > 0 Then Button2.SetFocus
        Stop Event
      Endif
  End Select
  
End

Public Sub Button2_KeyPress()
  
  Button2_Click()
  
End

Public Sub ChkPrx()
  
  If InStr("0123456789,.", Key.Text) = 0 Then
    If key.code <> key.BackSpace And Key.Code <> Key.tab And Key.Code <> Key.Delete And Key.code <> Key.enter And Key.code <> Key.return Then
      Stop Event
    Endif
  Endif
  
End

'********************************************
'* Saisie d'un fournisseur manuellement     *
'********************************************
Public Sub fourMan()
  
  Dim Rfours As Result
  
  Rfours = Utils.db.Exec("SELECT * FROM " & Cbase.Table("TabFour") & " WHERE fo_code = &1", four.Text)
  If Rfours.Available Then
    fournom.Text = Rfours!fo_nom
    b = 0
  Else
    If son Then
      Music.Play
    Endif
    Try Message.Warning("Ce fournisseur n'existe pas !", "OK")
    b = 1
  Endif
Catch
  If son Then
    Music.Play
  Endif
  Try message.Error(Error.Text & " " & Error.where)
  B = 1
  
End

'************************************************
'* On appelle la liste des fournisseurs         *
'************************************************
Public Sub Button4_Click()
  
  Dim Rfour As Result
  
  If fourlist.visible Then
    fourlist.clear
    fourlist.visible = False
  Else
    fourlist.visible = True
    fourlist.Columns.count = 2
    fourlist.Columns[0].Width = 65
    fourlist.Columns[1].Width = 180
    fourlist.Columns[0].Text = "code"
    fourlist.Columns[1].Text = "Intitulé"
    Rfour = Utils.db.Exec("SELECT * FROM " & Cbase.Table("TabFour") & " ")
    Do
      fourlist.Add(Rfour!fo_code, Rfour!fo_code)
      fourlist.Item[0] = Rfour!fo_code
      fourlist.Item[1] = Rfour!fo_nom
    Loop Until Rfour.MoveNext()
    fourList.MoveFirst
    fourList.SetFocus
    fourList.Item.Selected = True
  Endif
  
End

'**************************************************
'* Selection d'un fournisseur a la souris         *
'**************************************************
Public Sub fourList_Click()
  
  four.text = fourList.Item[0]
  fournom.text = fourList.Item[1]
  fourList.clear
  fourList.visible = False
  
End

'***************************************************
'* Selection d'un fournisseur manuellement         *
'***************************************************
Public Sub fourList_KeyPress()
  
  If Key.code = Key.Enter Or Key.code = Key.Return Then
    fourList_Click()
  Endif
  If Key.code = Key.Escape Or Key.code = Key.F2 Then
    fourList.visible = False
    fourList.clear
    four.SetFocus
    Stop Event
  Endif
  
End

Public Sub Button2_Click()
  
  Dim rArt As Result
  Dim i As Integer = 0
  
  With Utils
    rArt = Utils.db.Exec("SELECT * FROM " & Cbase.Table("TabArt") & " WHERE art_four = &1", Four.Text)
    If rArt.Available Then
      Message.Warning("Attention ! Durant cette opération les autres utilisateurs \n n'auront plus accès à la table des articles.")
      Utils.db.Exec("LOCK TABLES " & Cbase.Table("TabArt") & " WRITE, " & Cbase.Table("TabTvav") & " Write, " & Cbase.Table("TabEtiqp") & " Write, " & Cbase.Table("TabEtiqg") & " Write")
      Me.mouse = Mouse.Wait
      i = rArt.count
      If Tbrut.value = True Then
        Repeat
          Pbht = rArt!art_pbht
          nbdec = Rart!art_nbd
          nbdec = .Find_nbdec(nbdec)
          Pbht = Format$(Val(Utils.cpoint(Pbht)) + Val(Utils.cpoint(Pbht)) * Val(Utils.cpoint(Pourcent.Text)) / 100, nbdec)
          Remise = Rart!art_tr
          Pmp = Rart!art_pmp
          Txconv = Rart!art_txconv
          Frais = rArt!art_frais
          Coeff = rArt!art_coef
          Mtva = rArt!art_tva
          rTva()
          Prix_base()
          Calc_Percent()
          Calc_coeff()
          If Val(Utils.cpoint(rArt!art_qte)) > 0 And If rArt!art_etiq Then Maj_Etiqp(rArt!art_code, rArt!art_qte)
          Maj_Etiqg(rArt!art_code, rArt!art_qte)
          Utils.db.Exec("UPdate  " & Cbase.Table("TabArt") & "  SET  art_pbht = &1, art_tr = &2, art_pauaht =&3, art_pvht = &4, art_pvttc = &5, art_prvt = &6, art_paht = &7, art_coef = &8, art_ddatefbt = &{10} WHERE art_code = &9", Val(pbht), Val(remise), Val(Paua), Val(Pvht), Val(Pvttc), Val(Prvt), Val(paht), Val(Coeff), rArt!art_code, Format$(Now, "yyyymmdd"))
        Until rArt.MoveNext()
      Endif
      If Tnet.value = True Then
        Repeat
          Pvht = rArt!art_pvht
          nbdec = Rart!art_nbd
          nbdec = .Find_nbdec(nbdec)
          Pvht = Format$(Val(Utils.cpoint(Pvht)) + Val(Utils.cpoint(Pvht)) * Val(Utils.cpoint(Pourcent.Text)) / 100, nbdec)
          Prvt = Rart!art_prvt
          Pmp = Rart!art_pmp
          Mtva = rArt!art_tva
          rTva()
          Tva_Calcul()
          PvTTC_arrondi()
          PvTTcLF()
          Calc_Marge()
          Calc_Percent()
          Calc_coeff()
          If Val(Utils.cpoint(rArt!art_qte)) > 0 And If rArt!art_etiq Then Maj_Etiqp(rArt!art_code, rArt!art_qte)
          If Val(.cpoint(rArt!art_qte)) > 0 Then Maj_Etiqg(rArt!art_code, Val(.cpoint(rArt!art_qte)))
          Utils.db.Exec("UPdate  " & Cbase.Table("TabArt") & "  SET  art_pvht = &1, art_pvttc = &2, art_prvt = &3, art_coef = &4, art_ddatefbt = &6 WHERE art_code = &5", Val(Pvht), Val(Pvttc), Val(Prvt), Val(Coeff), rArt!art_code, Format$(Now, "yyyymmdd"))
        Until rArt.MoveNext()
      Endif
      Utils.db.Exec("UNLOCK TABLES")
      Me.Mouse = mouse.Default
      Message.Info("Traitement terminé ! " & i & " références ont été mises à jour !")
      If Etiqb = True Then
        If Message.Warning("Des étiquettes ont été préparées ! Voulez-vous les imprimer ?", "Oui", "Non") = 1 Then
          EtiqRecpt.Show
        Endif
      Endif
    Else
      Message.Info("Aucune mise à jour n'est à faire pour ce fournisseur !")
    Endif
  End With
  
End

'***********************************
'*   On gère le prix de base ht    *
'***********************************
Public Sub prix_base()
  
  With Utils
    pbht = Format$(Val(.cpoint(pbht)), nbdec)
    Trc()
    paht = Format$(Val(PaUA) / Val(.cpoint(Txconv)), nbdec)
    Prvt = Format(Val(paht) + Val(.cpoint(Frais)), nbdec)
    Pvht = Format$(Val(prvt) * Val(.cpoint(Coeff)), nbdec)
    Tva_Calcul()
    PvTTC_arrondi()
    PvTTcLF()
    Calc_Marge()
  End With
Catch
  If son Then
    Music.Play
  Endif
  message.Error(Error.Text & " " & Error.where)
  
End

'***********************************
'*  On gère le prix de vente HT    *
'***********************************
Public Sub PvTTcLF()
  
  Dim tx As Float
  
  tx = (1 + (Val(Utils.cpoint(Mtva)) / 100))
  Pvht = Format$(Val(PvTTc) / tx, nbdec)
  
End

'*********************************************
'*    On gère le taux de remise fournisseur  *
'*********************************************
Public Sub TrC()
  
  With Utils
    remise = .cpoint(remise)
    If Val(remise) = Null Then remise = "0"
    remise = Format$(Val(remise), "0.00")
    PaUA = Format$(Val(PbHT) - (Val(PbHT) * Val(remise) / 100), Nbdec)
  End With
Catch
  If son Then
    Music.Play
  Endif
  message.Error(Error.Text & " " & Error.where)
  
End

Public Sub Prix_achat()
  
  With Utils
    PaUA = .cpoint(PaUA)
    If Val(PaUA) = Null Then PaUA = "0"
    PaUA = Format$(Val(PaUA), nbdec)
    If Val(PaUA) <> "0" Then
      pbht = Format$(Val(PaUA) / (1 - (Val(remise) / 100)), nbdec)
      paht = Format$(Val(PaUA) / Val(Txconv), nbdec)
      Prvt = Format(Val(paht) + Val(Frais), nbdec)
      Pvht = Format$(Val(prvt) * Val(Coeff), nbdec)
      Tva_Calcul()
      PvTTC_arrondi()
      PvTTcLF()
      Calc_Marge()
    Endif
  End With
Catch
  If son Then
    Music.Play
  Endif
  message.Error(Error.Text & " " & Error.where)
  
End

'*****************************************
'*      Récupération de la tva           *
'*****************************************
Public Sub rTva()
  
  Dim Rtvaav As Result
  
  Rtvaav = Utils.db.Exec("SELECT * FROM " & Cbase.Table("TabTvav") & " WHERE code_tva = &1", Mtva)
  If Rtvaav.Available Then
    Mtva = Rtvaav!taux_tva
  Endif
  
End

'***************************************************************
'*   On calcule le montant de la tva et le prix de vente TTC   *
'***************************************************************
Public Sub Tva_Calcul()
  
  With Utils
    If PvTTc <> "" Or Pvht <> "" Then
      Mtva = Format$(Val(.cpoint(Mtva)), "00.00")
      PvTTc = Format$(Val(Pvht) + (Val(Pvht) * Val(Mtva) / 100), nbdec)
    End If
  End With
  
End

'***********************************
'*       On calcule la marge       *
'***********************************
Public Sub Calc_Marge()
  
  If IsNull(Pmp) Then Pmp = prvt
  If stocke Then
    Margem = Format$(Val(Pvht) - Val(Pmp), nbdec)
    Try Margep = Format$((Val(Margem) * 100) / Val(Pmp), nbdec)
  Else
    If Val(prvt) <> 0 Then
      Margem = Format$(Val(Utils.cpoint(Pvht)) - Val(Utils.cpoint(prvt)), nbdec)
      Try Margep = Format$((Val(Utils.cpoint(Pvht)) - Val(Pmp)) / Val(Utils.cpoint(Pvht)) * 100, nbdec)
    Endif
  Endif
  
End

'***********************************
'*        On gère l' arrondi       *
'***********************************
Public Sub PvTTC_arrondi()
  
  If Not PvTTc Then PvTTc = "0,00"
  Pvttc = Utils.cpoint(Pvttc)
  If arr = "0,05" Then
    If Right$(pvTTc) Like "[34567]*" Then
      PvTTC = Left$(PvTTC, (Len(PvTTC) - 1)) & "5"
    Else
      PvTTC = Round(Val(PvTTC), -1)
      PvTTC = Format$(PvTTC, nbdec)
    Endif
  Endif
  
  If arr = "0,10" Then
    PvTTC = Round(Val(PvTTC), -1)
    PvTTC = Format$(PvTTC, nbdec)
  Endif
  
  If arr = "0,50" Then
    If Val(pvTTc) <= Val(Left$(pvTTc, (Len(pvTTc) - 2)) & 25) Then
      PvTTC = Left$(PvTTC, (Len(PvTTC) - 2)) & "00"
    Else
      If Val(pvTTc) <= Val(Left$(pvTTc, (Len(pvTTc) - 2)) & 75) Then
        PvTTC = Left$(PvTTC, (Len(PvTTC) - 2)) & "50"
      Endif
      If Val(pvTTc) >= Val(Left$(pvTTc, (Len(pvTTc) - 2)) & 76) Then
        PvTTC = Round(Val(PvTTC))
        PvTTC = Format$(PvTTC, nbdec)
      Endif
    Endif
  Endif
  
  If arr = "1,00" Then
    PvTTC = Round(Val(PvTTC))
    PvTTC = Format$(PvTTC, nbdec)
  Endif
  
End

'********************************************
'*       On calcule le taux de marque       *
'********************************************
Public Sub Calc_Percent()
  
  Try Percent = Format$(((Val(Utils.cpoint(Pvht)) - Val(Utils.cpoint(prvt))) / Val(Utils.cpoint(Pvht))) * 100, "0.000")
  
End

'***************************************************
'*        On calcule le coefficient de marge       *
'***************************************************
Public Sub Calc_coeff()
  
  Try Coeff = Format$(1 / (1 - (Val(Percent)) / 100), "0.0000")
  
End

'***********************************On gère les étiquettes*****************************
Public Sub Maj_Etiqp(Cart As String, nb As Integer)
  
  Dim Etiqp As Result
  
  Etiqp = Utils.db.Exec("SELECT * FROM " & Cbase.Table("TabEtiqp") & " where code = &1", Cart)
  If Etiqp.Available Then
    Utils.db.Exec("UPdate  " & Cbase.Table("TabEtiqp") & "  SET code = &1, nombre = &2 WHERE code = &1", Cart, nb)
  Else
    Utils.db.Exec("INSERT INTO " & Cbase.Table("TabEtiqp") & "(code,nombre) VALUES (&1, &2)", Cart, nb)
  Endif
  Etiqb = True
  
End

Public Sub Maj_Etiqg(Cart As String, nb As Integer)
  
  Dim Qteetiq As Integer
  Dim Etiqg As Result
  
  Etiqg = Utils.db.Exec("SELECT * FROM " & Cbase.Table("TabEtiqg") & " where code = &1", cart)
  If Not Etiqg.Available Then
    Utils.db.Exec("INSERT INTO " & Cbase.Table("TabEtiqg") & "(code,nombre) VALUES (&1, &2)", Cart, 1)
  Endif
  Etiqb = True
  
End
